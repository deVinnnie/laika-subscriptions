plugins {
    id 'java'
    id 'groovy'
    id 'jacoco'
    id 'idea'
    id 'org.springframework.boot' version '2.2.13.RELEASE'
    id "com.bertramlabs.asset-pipeline" version '3.3.2'
    id 'com.github.kt3k.coveralls' version '2.8.2'
}

apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

jar {
    baseName = 'laika-subscriptions'
    version =  '1.0-SNAPSHOT'
}

ext {
    springBootVersion = '2.2.13.RELEASE'
    seleniumVersion = '3.141.59'
    gebVersion = "4.1"
}

group = 'be.mira.jongeren'
version = '1.0-SNAPSHOT'

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

test{
    useJUnitPlatform()

    dependsOn 'cleanTest'

    testLogging {
        events "passed", "skipped", "failed"
    }
}

wrapper {
    gradleVersion = '6.9'
}

// Heroku Stuff
task stage(dependsOn: ['assemble']) {
}

build.mustRunAfter clean
bootJar.mustRunAfter assetCompile

repositories {
     maven { url "https://repo.maven.apache.org/maven2" }
}

sourceSets {
    integrationTest {
        groovy{
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDirs file('src/integration-test/groovy')
        }
        resources.srcDir file('src/integration-test/resources')
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

configurations {
    integrationTestCompile.extendsFrom testImplementation
    integrationTestRuntime.extendsFrom testRuntime
}

task integrationTest(type: Test) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
}

dependencies {
    implementation "io.micronaut:micronaut-inject:1.3.4"

    implementation 'com.github.jknack:handlebars-springmvc:4.2.0'

    // Spring Boot
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'org.springframework.boot:spring-boot-starter-mail'
    implementation 'org.springframework.boot:spring-boot-starter-security'
//    implementation 'org.springframework.boot:spring-boot-devtools'

    // DB
    implementation group: 'com.h2database', name: 'h2', version:'1.4.200'
    implementation group: 'org.postgresql', name: 'postgresql', version:'42.2.18'

    // Other
    //implementation group: 'ro.pippo', name: 'pippo-jade', version:'1.12.0'

    // CSS Libs: PureCSS
    assets group: 'org.webjars.npm', name: 'purecss', version:'0.6.0'

    // Test Dependencies
    testImplementation group: 'junit', name: 'junit', version:'4.12'
    testImplementation group: 'com.icegreen', name: 'greenmail', version:'1.5.5'
    testImplementation group: 'com.ninja-squad', name: 'DbSetup', version:'2.1.0'
    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', version:"${springBootVersion}"
    //testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.3.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.2'

    // Assets Pipeline
    assets('com.bertramlabs.plugins:sass-asset-pipeline:3.3.2'){
        exclude group: 'log4j', module: 'log4j'
    }
    implementation 'com.google.code.gson:gson:2.8.6' // Fixes conflict with Spring-Boot

    integrationTestCompile group: 'org.seleniumhq.selenium', name: 'selenium-java', version:"${seleniumVersion}"
    integrationTestCompile  'com.google.code.gson:gson:2.8.6' // Fixes conflict with Spring-Boot

    // ---------------------------------------------
    // Geb
    // ---------------------------------------------
    integrationTestCompile 'org.w3c.css:sac:1.3' // Magic
    integrationTestCompile "org.seleniumhq.selenium:htmlunit-driver:2.36.0"
    integrationTestCompile "org.seleniumhq.selenium:selenium-chrome-driver:${seleniumVersion}"
    integrationTestCompile "org.gebish:geb-junit4:${gebVersion}"
}

assets {
    compileDir = "${buildDir}/resources/main/static/assets"

    minifyJs = true
    minifyCss = true
    enableSourceMaps = true
    enableDigests = false
    enableGzip = false
    maxThreads = 1

    includes = ["fonts/*"]
    excludes = ['**/build/*','**/*.json', '**/*.md']

    //developmentRuntime can be turned off
    developmentRuntime=false
}

jacoco {
    toolVersion = "0.8.2"
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

bootRun {
    jvmArgs = ["-Dspring.profiles.active=development,disable-security,mock"]
    sourceResources sourceSets.main
}

idea {
    module {
        //and some extra test source dirs
        testSourceDirs += file('src/integration-test/java')
        testSourceDirs += file('src/integration-test/groovy')
        //generatedSourceDirs += file('some-extra-source-folder')
        //scopes.TEST.plus += [ configurations.integrationTest ]
        scopes.TEST.plus += [ configurations.integrationTestCompile ]
        scopes.TEST.plus += [ configurations.integrationTestRuntime ]

    }
}
