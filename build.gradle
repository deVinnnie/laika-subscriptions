plugins {
    id 'java'
    id 'jacoco'
    id 'idea'
    id 'org.springframework.boot' version '1.5.2.RELEASE'
    id "com.bertramlabs.asset-pipeline" version '2.13.1'
    id 'com.github.kt3k.coveralls' version '2.8.1'
}

jar {
    baseName = 'laika-subscriptions'
    version =  '1.0-SNAPSHOT'
}

group = 'be.mira.jongeren'
version = '1.0-SNAPSHOT'

description = """"""

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

tasks.matching {it instanceof Test}.all {
    testLogging{
        events = ["failed", "passed", "skipped"]

        afterSuite { desc, result ->
            // Match the outermost suite
            if (!desc.parent) {
                println "Tests run: ${result.testCount} Failures: ${result.failedTestCount} Skipped: ${result.skippedTestCount}"
            }
        }


        // Make sure output from
        // standard out or error is shown
        // in Gradle output.
        //showStandardStreams = true
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.5'
}

repositories {
     maven { url "http://repo.maven.apache.org/maven2" }
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDirs file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
}

dependencies {
    // Spring Boot
    compile(group: 'org.springframework.boot', name: 'spring-boot-starter', version:'1.5.2.RELEASE') {
        exclude(module: 'logback-classic')
    }
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa', version:'1.5.2.RELEASE'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web', version:'1.5.2.RELEASE'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-aop', version:'1.5.2.RELEASE'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-mail', version:'1.5.2.RELEASE'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-security', version:'1.5.2.RELEASE'
    compile group: 'org.springframework.boot', name: 'spring-boot-devtools', version:'1.5.2.RELEASE'

    // DB
    compile group: 'com.h2database', name: 'h2', version:'1.4.192'
    compile group: 'org.xerial', name: 'sqlite-jdbc', version:'3.15.1'

    // Other
    compile group: 'commons-lang', name: 'commons-lang', version:'2.6'
    compile group: 'ro.pippo', name: 'pippo-jade', version:'1.3.0'

    // CSS Libs: PureCSS & Foundation
    //compile group: 'org.webjars', name: 'webjars-locator', version:'0.30'
    compile group: 'org.webjars.npm', name: 'purecss', version:'0.6.0'
    //compile group: 'org.webjars.npm', name: 'foundation-sites', version:'6.3.1'

    // Test Dependencies
    testCompile group: 'junit', name: 'junit', version:'4.12'
    testCompile group: 'com.icegreen', name: 'greenmail', version:'1.5.5'
    testCompile group: 'com.ninja-squad', name: 'DbSetup', version:'2.1.0'
    testCompile(group: 'org.springframework.boot', name: 'spring-boot-starter-test', version:'1.5.2.RELEASE') {
        exclude(module: 'commons-logging')
    }

    integrationTestCompile group: 'org.seleniumhq.selenium', name: 'selenium-java', version:'3.3.1'
    integrationTestCompile group: 'org.seleniumhq.selenium', name: 'selenium-support', version:'3.3.1'

    // Assets Pipeline
    //runtime 'com.bertramlabs.plugins:asset-pipeline-spring-boot:2.13.1'
    assets 'com.bertramlabs.plugins:sass-asset-pipeline:2.13.1'
}

assets {
    compileDir = "${buildDir}/resources/main/static/assets"

    minifyJs = true
    minifyCss = true
    enableSourceMaps = true
    enableDigests = false
    enableGzip = true
    maxThreads = 2
    
    includes = ["fonts/*"]

    //developmentRuntime can be turned off
    developmentRuntime=true
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

bootRun {
    addResources = true
}

idea {
    module {
        //and some extra test source dirs
        testSourceDirs += file('src/integration-test/java')
        //generatedSourceDirs += file('some-extra-source-folder')
        //scopes.TEST.plus += [ configurations.integrationTest ]
        scopes.TEST.plus += [ configurations.integrationTestCompile ]
        scopes.TEST.plus += [ configurations.integrationTestRuntime ]

    }
}
